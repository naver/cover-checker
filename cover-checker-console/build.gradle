plugins {
    id 'application'
    id 'org.graalvm.buildtools.native' version '0.11.1'
}

application {
    mainClass = 'com.naver.nid.cover.Launcher'
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.naver.nid.cover.Launcher'
        attributes 'Description': 'Check your test coverage for new appeded codes'
    }
    baseName = 'cover-checker'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

build {
    dependsOn fatJar
}

dependencies {
    implementation project(':cover-checker-cobertura')
    implementation project(':cover-checker-github')
    implementation project(':cover-checker-core')
    implementation project(':cover-checker-jacoco')
    implementation project(':cover-checker-lcov')

    implementation 'commons-cli:commons-cli:1.4'
    testImplementation 'org.eclipse.mylyn.github:org.eclipse.egit.github.core:5.4.0.201905081430-m2'
}

graalvmNative {
    binaries.all {
        resources.autodetect()
    }

    binaries.main {
        // Optional native-image configuration
        imageName.set('cover-checker')
        mainClass = 'com.naver.nid.cover.Launcher'
        buildArgs.addAll(
            ["-O0",
            "--static",
            "--gc=epsilon",
            '-H:+ReportExceptionStackTraces',
            '-H:IncludeResources=logback.xml',
            '-H:IncludeResources=pattern.xml',
            "-R:MaxHeapSize=100m",
            // "--link-at-build-time",
            '--initialize-at-build-time=org.slf4j,ch.qos.logback',
            '--initialize-at-build-time=org.apache.commons.cli',
            '--initialize-at-run-time=com.naver.nid.cover',

            // HTTP 클라이언트 관련
            '--enable-url-protocols=http,https',
            '--enable-all-security-services'])

        debug = true
        verbose = true
        useFatJar = true
    }

    agent {
        enabled = true
    }
}
