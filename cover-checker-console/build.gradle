plugins {
    id 'application'
    id 'org.graalvm.buildtools.native' version '0.11.1'
}

mainClassName = 'com.naver.nid.cover.Launcher'

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.naver.nid.cover.Launcher'
        attributes 'Description': 'Check your test coverage for new appeded codes'
    }
    baseName = 'cover-checker'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

build {
    dependsOn fatJar
}

dependencies {
    implementation project(':cover-checker-cobertura')
    implementation project(':cover-checker-github')
    implementation project(':cover-checker-core')
    implementation project(':cover-checker-jacoco')
    implementation project(':cover-checker-lcov')

    implementation 'commons-cli:commons-cli:1.4'
    testImplementation 'org.eclipse.mylyn.github:org.eclipse.egit.github.core:5.4.0.201905081430-m2'
}

graalvmNative {
    binaries.main {
        // Optional native-image configuration
        imageName.set('cover-checker')
        mainClass.set('com.naver.nid.cover.Launcher')
        debug = true
    }
    // Provide native-image arguments if needed
    // args.set(['--no-fallback'])
}
